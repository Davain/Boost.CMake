##########################################################################
# Copyright (C) 2011 Daniel Pfeifer <daniel@pfeifer-mail.de>             #
#                                                                        #
# Distributed under the Boost Software License, Version 1.0.             #
# See accompanying file LICENSE_1_0.txt or copy at                       #
#   http://www.boost.org/LICENSE_1_0.txt                                 #
##########################################################################

# - Config file for the Boost package
# It defines the following variables
#  BOOST_INCLUDE_DIRS - include directories for Boost
#  BOOST_LIBRARY_DIRS - library directories for Boost (normally not used!)
#  BOOST_LIBRARIES    - libraries to link against
#  BOOST_USE_FILE     -

set(Boost_FOUND TRUE)
set(Boost_INCLUDE_DIRS "@BOOST_INCLUDE@")
set(Boost_LIBRARY_DIRS "@BOOST_LIBRARIES@")
set(Boost_RESOURCE_PATH "@BOOST_RESOURCES@")
set(Boost_MODULE_PATH "@BOOST_MODULES@")
set(Boost_USE_FILE "${Boost_MODULE_PATH}/UseBoost.cmake")
set(Boost_DEV_FILE "${Boost_MODULE_PATH}/UseBoostDev.cmake")

set(Boost_COMPONENTS_FOUND)
set(Boost_COMPONENTS_MISSING)

foreach(component ${Boost_FIND_COMPONENTS})
  string(TOUPPER ${component} COMPONENT)
  set(component_file "@CMAKE_INSTALL_PREFIX@/@BOOST_CMAKE_DIR@/components/${component}.cmake")
  if(BOOST_MAINTAIN_${COMPONENT})
    set(Boost_${COMPONENT}_FOUND TRUE)
  elseif(EXISTS "${component_file}")
    include("${component_file}")
    set(Boost_${COMPONENT}_FOUND TRUE)
    list(APPEND Boost_COMPONENTS_FOUND ${component})
  else()
    set(Boost_FOUND FALSE)
    set(Boost_${COMPONENT}_FOUND FALSE)
    list(APPEND Boost_COMPONENTS_MISSING ${component})
  endif()
endforeach(component)

if(Boost_COMPONENTS_FOUND)
  message(STATUS "Found the following Boost components:")
  foreach(component ${Boost_COMPONENTS_FOUND})
    message(STATUS "  ${component}")
  endforeach(component)
endif(Boost_COMPONENTS_FOUND)

if(Boost_COMPONENTS_MISSING)
  message(STATUS "The following Boost components are missing:")
  foreach(component ${Boost_COMPONENTS_MISSING})
    message(STATUS "  ${component}")
  endforeach(component)
endif(Boost_COMPONENTS_MISSING)
